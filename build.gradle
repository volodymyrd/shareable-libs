// SonarLint
plugins {
    id "name.remal.sonarlint" version "1.2.2"
}

defaultTasks 'clean', 'build'

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: "name.remal.sonarlint"

    version = '0.0.1-SNAPSHOT'

    ext {
        versions = [
                assertj: '3.19.0',
                junit  : '5.7.0',
                guava  : '30.1-jre',
        ]

        libs = [:]

        libs.guava = ["com.google.guava:guava:${versions.guava}"]

        libs.test = ["org.assertj:assertj-core:${versions.assertj}",
                     "org.junit.jupiter:junit-jupiter-api:${versions.junit}",
                     "org.junit.jupiter:junit-jupiter-engine:${versions.junit}",
                     "org.junit.jupiter:junit-jupiter-params:${versions.junit}"
        ]
    }

    sourceCompatibility = targetCompatibility = 1.8

    compileJava {
        options.encoding = "UTF-8"
    }

    compileTestJava {
        options.encoding = "UTF-8"
    }

    repositories {
        mavenCentral()
    }

    task wrapper(type: Wrapper) {
        gradleVersion = '6.8'
    }

    test {
        useJUnitPlatform()

        testLogging {
            afterSuite { desc, result ->
                if (!desc.parent) { // will match the outermost suite
                    println "Results: " +
                            "${result.resultType} (${result.testCount} tests, " +
                            "${result.successfulTestCount} successes, ${result.failedTestCount} failures, " +
                            "${result.skippedTestCount} skipped)"
                }
            }
        }
    }

    publishing {
        repositories {
            maven {
                name = "Shareable-Libraries"
                url = uri("https://maven.pkg.github.com/volodymyrd/shareable-libs")
                credentials {
                    // ~/.gradle/gradle.properties
                    username = project.findProperty("repo.user") ?: System.getenv("USERNAME")
                    password = project.findProperty("mvn_repo_rw.key") ?: System.getenv("TOKEN")
                }
            }
        }
    }
}
